import std/os;

let MICROSECOND = 1000;
let MILLISECOND = MICROSECOND * 1000;
let SECOND = MILLISECOND * 1000;
let HOUR = SECOND * 3600;
let DAY = HOUR * 24;
let YEAR = DAY * 365;

self.MICROSECOND = MICROSECOND;
self.MILLISECOND = MILLISECOND;
self.SECOND = SECOND;
self.HOUR = HOUR;
self.DAY = DAY;
self.YEAR = YEAR;

let MONTHS = [
    31, // January
    28, // February
    31, // March
    30, // April
    31, // May
    30, // June
    31, // July
    31, // August
    30, // September
    31, // October
    30, // November
    31  // December
];

fn to_delta(delta) {
    if delta is Delta {
        return delta;
    }

    Delta(delta)
}

pub class Delta {
    fn init(self, nsecs) {
        self.nsecs = nsecs;
    }

    fn secs(self) => self.nsecs / SECOND;
    fn usecs(self) => self.nsecs / MICROSECOND;
    fn msecs(self) => self.nsecs / MILLISECOND;
    fn hours(self) => self.nsecs / HOUR;
    fn days(self) => self.nsecs / DAY;
}

pub class Time {
    fn init(self, unix) {
        self._unix = unix;
        self.ymd = parse_ymd(unix);
    }

    fn year(self) => self.ymd[0];
    fn month(self) => self.ymd[1];
    fn day(self) => self.ymd[2];
    fn unix(self) => self._unix;
    fn diff(self, other) => Delta(self._unix - other._unix);
}

fn div_by(n, div) => n % div == 0;

pub fn is_leap_year(year) => div_by(year, 4) && !(
    div_by(year, 100) &&
    !div_by(year, 400)
);

pub fn days_in_month(year, month) {
    let days = MONTHS[month - 1];

    if month == 2 && is_leap_year(year) {
        return days + 1;
    }

    days
}

fn parse_ymd(unix) {
    let year = 1970;

    // Calculate the number of years (taking leap years into account)
    for unix >= YEAR {
        unix -= YEAR;
        year += 1;

        // Leap year
        if is_leap_year(year) {
            unix -= DAY;
        }
    }

    // Now, let's move on to the month/day
    let month = 1;
    let day = 1;

    for unix >= DAY {
        unix -= DAY;
        day += 1;

        if day > days_in_month(year, month) {
            day = 1;
            month += 1;
        }
    }

    [year, month, day]
}

// Get the current time in nanoseconds since the Unix epoch
pub fn unix() {
    let td = os.gettimeofday();
    (td.secs * SECOND) + (td.usecs * 1000)
}

// Sleep for a given duration
pub fn sleep(input) => os.nanosleep(to_delta(input).nsecs);

// Get the current date and time in UTC
pub fn now() {
    Time(unix())
}
